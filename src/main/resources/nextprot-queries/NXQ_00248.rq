#id:NXQ_00248
#title:Variants with phenotype annotation that map to a 3D structure
#tags:3D structure,phenotype,variant,snorql-only,tutorial
#acs:P38398,Q04771,P25054...
#count:7600
#time:23

select distinct ?entry (str(?pfname) as ?phenovar) (concat(str(sample(?pdbac)),"...") as ?pdbsample) where {
  ?entry :gene / :name / rdfs:label ?gen .
  ?entry :isoform ?iso.
  ?iso  :proteoform ?pf .
  ?pf rdfs:label ?pfname .
  ?pf :phenotypicVariation ?pv1 .
  ?pf :difference ?pfmod .
  ?pfmod a :Variant .
  ?pfmod :start ?vstart; :end ?vend .
  ?iso :pdbMapping  ?pdbmap.
  ?pdbmap rdfs:comment ?pdbac.
  ?pdbmap :start ?pdbstart ; :end ?pdbend.
  filter(?pdbstart <= ?vstart && ?pdbend >= ?vend ) 
} group by ?entry ?pfname ?pdbsample
