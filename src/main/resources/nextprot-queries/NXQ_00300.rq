#id:NXQ_00300
#title:Proteins with protein existence "At protein level" (PE=1) that have no function annotated, are highly expressed in brain and have homologs in Drosophila melanogaster according to OrthoDB
#comment:combination of NXQ_00004 and NXQ_00022, plus federation with OrthoDB
#tags:federated query,expression,function,gene,name,phylogeny,protein existence,snorql-only,tutorial
#acs:Q5SZI1,Q8IV50,A1L170
#count:44

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX lscr: <http://purl.org/lscr#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX odb: <http://purl.orthodb.org/>

select distinct ?entry (str(?name) as ?human_name) (group_concat(distinct str(?fly_name); SEPARATOR = ",") as ?fly_names) where
{
?entry :isoform ?iso.
?entry :gene / :recommendedName / rdfs:label ?name.
?entry :existence :Evidence_at_protein_level .
filter not exists { ?iso :functionInfo ?_. }
filter not exists { ?iso :catalyticActivity ?_ .} 
filter not exists { ?iso :transportActivity ?_ .} 
filter not exists { ?iso :pathway ?_. }
filter not exists { ?iso :function / :term ?fterm . filter(?fterm != cv:GO_0005524 && ?fterm != cv:GO_0000287 && ?fterm != cv:GO_0005515 && ?fterm != cv:GO_0042802 && ?fterm != cv:GO_0008270 && ?fterm != cv:GO_0051260 && ?fterm != cv:GO_0005509 && ?fterm != cv:GO_0003676 && ?fterm != cv:GO_0003824 && ?fterm != cv:GO_0007165 && ?fterm != cv:GO_0035556 && ?fterm != cv:GO_0046914 && ?fterm != cv:GO_0046872)}
?iso :expression ?e1.
?e1 :term/:childOf cv:TS-0095;:evidence/:observedExpression :High.
{
service <http://sparql.orthodb.org/sparql>
{?gene rdfs:seeAlso ?entry; odb:memberOf ?og.
?og odb:ogBuiltAt [up:scientificName ?clade]
; odb:hasMember ?ortholog.
?ortholog odb:name ?fly_name; up:organism/a/up:scientificName 'Drosophila melanogaster'.
filter (?clade='Metazoa') }
}
}
