#id:NXQ_00272
#title:Proteins involved in coronaviruses/SARS-CoV-2 pathways with associated medical information
#comment:Federated query with WikiPathways
#tags:disease,federated query,pathway,snorql-only,tutorial
#acs:O14920,O15111
#count:125
#time:11

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

select distinct ?entry (str(?gen) AS ?gene) ?pathwayname  (str(?discom) AS ?discom1) ?medsource where {
    service <http://sparql.wikipathways.org/sparql> {
        {?geneProduct a wp:Protein}
            union
        {?geneProduct a wp:GeneProduct}

        ?geneProduct rdfs:label ?genraw .
        bind (concat( ""^^xsd:string, ?genraw) as ?gen).
        filter(!regex(?gen,"[ a-z-]")). # ensures official gene names for subsequent neXtprot matching

        ?geneProduct dcterms:isPartOf ?pathway .
        ?pathway a wp:Pathway .
        ?pathway wp:organism ?organism .
        filter(contains(str(?organism),"9606"))

        ?pathway dc:title ?pathwayname .
        filter(regex(?pathwayname,"sars-cov-2","i")|| regex(?pathwayname,"corona","i") ).
   }

   ?entry a :Entry .
   ?entry :gene / :recommendedName / rdfs:label ?gen .
   ?entry :isoform ?iso.
   ?iso :medical ?med.
   ?med rdfs:comment ?discom.
   ?med :evidence/:assignedBy ?medsource.
}
order by ?entry
