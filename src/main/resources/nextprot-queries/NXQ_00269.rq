#id:NXQ_00269
#title:Proteins with associated cancer pathways in WikiPathways (via Disease Ontology classification)
#tags:federated query,pathway,snorql-only,tutorial
#acs:Q13131,Q9HBH9,P31749
#count:1826 (1059 entries and 36 pathways)
#time:3

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

select distinct ?entry (str(?gen) as ?gene) ?pathwayname where {
    service <http://sparql.wikipathways.org/sparql> {
        {?geneProduct a wp:Protein}
            union
        {?geneProduct a wp:GeneProduct}
        ?geneProduct rdfs:label ?genraw .
        bind (concat( ""^^xsd:string, ?genraw) as ?gen).
        filter(!regex(?gen,"[ a-z-]")). # ensures official gene names for subsequent neXtprot matching
        ?geneProduct dcterms:isPartOf ?pathway .
        ?pathway a wp:Pathway .
        ?pathway wp:organism ?organism .
        filter(contains(str(?organism),"9606")) # Human proteins
        ?pathway dc:title ?pathwayname .
        ?pathway wp:ontologyTag <http://purl.obolibrary.org/obo/DOID_162> . # Parent id for all cancers
    }
    ?entry a :Entry .
    ?entry :gene / :recommendedName / rdfs:label ?gen .
}
order by ?pathwayname
