#id:NXQ_00271
#title:Polymorphisms located on ACE2 and TMPRSS2 and affecting proteinsâ€™ activity, structure, PTM...
#tags:snorql-only,variant,tutorial
#acs:Q9BYF1,O15393
#count:26
#time:6

select distinct (str(?gn) as ?gene) ?pos (str(?snpac) as ?varid) (str(?orgaa) as ?orgAA) ?annot_type  (str(?txt) as ?note) (str(?varaa) as ?varAA) ?freq where
{
 values ?entry {entry:NX_Q9BYF1 entry:NX_O15393} # proteins of interest (ACE2, TMPRSS2)
  ?entry :gene /:recommendedName / rdfs:label ?gn.
  ?entry :isoform ?iso .
  ?iso :swissprotDisplayed true; :variant ?var .
  ?var :start ?pos ; :original ?orgaa; :variation ?varaa .
  optional {?var :evidence / :alleleFrequency ?freq .}
  ?var :evidence / :reference ?xref .
  ?iso :positionalAnnotation ?annot .
  optional {?annot rdfs:comment ?txt .}
  ?annot a ?annot_type .
  {
   ?annot :start ?pos; :end ?pos.
   filter not exists {?annot a :Variant. }
   filter not exists {?annot a :SequenceConflict. }
  }
  union
  {
   ?annot a :DisulfideBond.
   {?annot  :start ?pos. }
    union
   {?annot  :end ?pos. }
  }
 } order by ?entry ?pos
