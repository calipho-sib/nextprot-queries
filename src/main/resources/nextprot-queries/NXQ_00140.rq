#id:NXQ_00140
#title:Proteins that interact with viral proteins
#tags:PPI,federated query,tutorial
#acs:A0A075B6T6,A6NCC3,O00505
#count:1057
#time:30-45

PREFIX up:<http://purl.uniprot.org/core/>
PREFIX taxon:<http://purl.uniprot.org/taxonomy/>

select distinct ?entry where {
  ?entry :isoform ?iso.
  optional {?iso :interactionInfo ?itinfo .}
  {
  ?itinfo rdfs:comment ?txt.
  filter(contains(?txt,"viral") || contains(?txt,"virus")).
  } # refers to a virus in the free-text interactionInfo topic
UNION
  {  ?iso :uniprotKeyword / :term cv:KW-0945.} # host-virus interaction keyword
UNION
  {
   SERVICE <http://sparql.uniprot.org/sparql> {
     SELECT distinct ?viralinteractor WHERE # get viral proteins with an IntAct xref
    {
	?viralinteractor a up:Protein .
	?viralinteractor rdfs:seeAlso ?db .
	?db up:database <http://purl.uniprot.org/database/IntAct> .
  	?viralinteractor up:organism ?tax .
	?tax up:scientificName ?orgname.
	filter(contains(?orgname,"virus"))
    }
   }
  ?entry :isoform / :binaryInteraction ?it .
  ?it :interactant ?interactant; :quality :GOLD. # NeXtprot entries with a GOLD IntAct binary interaction
  ?interactant skos:exactMatch ?viralinteractor . # interactant must be in the uniprot service result set to select the entry
  } 
UNION
  {
  SERVICE <http://sparql.uniprot.org/sparql> {
     SELECT distinct ?humprotein WHERE # get human proteins that share a PDB xref with a viral protein (same PDB id)
    {
	?humprotein a up:Protein .
    ?humprotein up:organism taxon:9606 . 
	?humprotein rdfs:seeAlso ?db .
	?db up:database <http://purl.uniprot.org/database/PDB> .
	?viralprotein a up:Protein .
	?viralprotein rdfs:seeAlso ?db .
    ?viralprotein up:organism ?tax .
	?tax up:scientificName ?orgname.
	filter(contains(?orgname,"virus"))
    }
   }
  ?entry skos:exactMatch ?humprotein .	
  }  
}
order by ?entry