#id:NXQ_00023
#title:Proteins that are involved in transport and located in a membrane and that are not glycosylated (experimentally or predicted)
#tags:function,PTM,glycosylation,subcellular location,tutorial
#acs: A0AVI4,A0FGR8,A4IF30...
#count:825
#time:0.7

select distinct ?entry where {
  ?entry :isoform  ?iso .
  {
    ?iso :goBiologicalProcess ?gofunc .
	?gofunc :term / :childOf cv:GO_0006810. # GO value for transport
	filter not exists {?gofunc  :negativeEvidence ?negev} # No negative function evidence
  } union {
    ?iso :uniprotKeyword / :term cv:KW-0813. # KW for transport
  }
  {   
    ?iso :uniprotKeyword / :term cv:KW-0812. # transmembrane
  } union {
     ?iso :topology / :term cv:CVTO_0004. # intramembrane
  }  
  filter not exists { ?iso :uniprotKeyword / :term cv:KW-0325 } # not a glycoprotein
}
