#id:NXQ_00276
#title:Diseases/phenotypes associated with coding variants and associated publications for a given gene
#comment:Federated query with DisGeNET
#tags:disease,federated query,snorql-only,variant,tutorial
#acs:P68871
#count:99
#time:12

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX so:<http://purl.obolibrary.org/obo/SO_>

select distinct ?entry (str(?umlsTerm) as ?umlsTerm_str) (str(?snpac) as ?varid) ?pos (str(?orgaa) as ?orgAA) (str(?varaa) as ?varAA) ?article where {
  service <http://rdf.disgenet.org/sparql/> {
    ?vda sio:SIO_000628 ?umls, ?variant .
    ?vda sio:SIO_000772 ?article .
    ?umls dcterms:title ?umlsTerm.
    {?umls a sio:SIO_010299 .} # disease
        union
    {?umls a sio:SIO_010056 .} # or phenotype
    ?variant a so:0001583 ; dcterms:title ?variantTitle . # Missense variant
    ?variant so:associated_with ?gene .
    ?gene a ncit:C16612; sio:SIO_010078 ?protein .
    ?gene sio:SIO_000205 ?gname.
    filter(contains(str(?gname),"HBB"))     # Hemoglobin gene (NX_P68871)
  }

  BIND(IRI(replace(str(?protein),"purl","www")) AS ?unipage) .
  ?entry :swissprotPage ?unipage .
  ?entry :isoform ?iso .
  ?iso :swissprotDisplayed true .
  ?iso :variant ?var .
  ?var :evidence /:reference ?xref .
  ?xref :provenance db:dbSNP; :accession ?snpac .
  ?var :start ?pos ; :original ?orgaa; :variation ?varaa .
  filter(contains(?snpac,str(?variantTitle))) # matches the exact same variant Disgenet returned
}
