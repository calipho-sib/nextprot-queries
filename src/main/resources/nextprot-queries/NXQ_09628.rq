#id:NXQ_09628
#title:Check there are no entries in the Ion channels variants portal that do not have the GO molecular function Voltage-gated sodium channel activity (GO:0005248)
#tags:QC,portal
#acs:
#count:0
#time:0.848
 
select distinct ?entry where {
  values ?gene
  {"SCN1A" "SCN2A" "SCN3A" "SCN4A" "SCN5A" "SCN8A" "SCN9A" "SCN10A" "SCN11A"} # space-separated
   
  ?entry :gene / :name  / rdfs:label ?genename .
  bind (str(?genename) as ?gn) # here we convert "xxx"^^xsd:string to "xxx" to match the values
  filter(?gn = ?gene)
  filter not exists {?entry :isoform /:goMolecularFunction /:term cv:GO_0005248.} # Voltage-gated sodium channel activity
}
