#id:NXQ_00274
#title:Proteins involved in coronaviruses/SARS-CoV-2 pathways that are drug targets according to DrugBank
#comment:Federated query with DrugBank and WikiPathways
#tags:drug,federated query,pathway,snorql-only
#acs:O14727,O14920,P01375 
#count:22
#time:26

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX drugbank: <http://wifo5-04.informatik.uni-mannheim.de/drugbank/resource/drugbank/>

select ?entry (str(?gen) AS ?gene) ?pathwayname   (group_concat(distinct ?druglab ; SEPARATOR = ",") AS ?drugx) where {
    service <http://sparql.wikipathways.org/sparql> {

        {?geneProduct a wp:Protein}
            union
        {?geneProduct a wp:GeneProduct}

        ?geneProduct rdfs:label ?gen .
        filter(!regex(?gen,"[ a-z-]")). # ensures official gene names for subsequent neXtprot matching

        ?geneProduct dcterms:isPartOf ?pathway .
        ?pathway a wp:Pathway .
        ?pathway wp:organism ?organism .
        filter(contains(str(?organism),"9606"))

        ?pathway dc:title ?pathwayname .
        filter(regex(?pathwayname,"corona","i") || regex(?pathwayname,"sars-cov-2","i")).

    }
    ?entry a :Entry .
    ?entry :gene / :name ?gen .
    ?entry :swissprotPage ?unipage.
    service <http://wifo5-03.informatik.uni-mannheim.de/drugbank/sparql> {
        ?drug rdfs:label ?druglab; drugbank:target /drugbank:swissprotPage ?unipage.
    }
}
group by ?entry ?gen ?pathwayname
order by ?entry
