#id:NXQ_00283
#title:Proteins with no reported mitochondrial localization but interacting with 20 or more mitochondrial proteins ('gold' quality) 
#tags:PPI,snorql-only,subcellular location,tutorial
#acs:P42858,Q6RW13,Q96DZ9
#count:17
#time:210.058

select distinct ?entry (str(?gen) as ?gene) (count(distinct ?mitopartner) as ?mitcnt) (count(distinct ?partner) as ?itcnt) where {
values ?mitloc {cv:GO_0005739 cv:SL-0173} # GO and SL values for mitochondrion
 ?entry :isoform ?iso.
  ?entry :gene / :recommendedName / rdfs:label ?gen.
 filter not exists { ?iso :cellularComponent /:term / :childOf ?mitloc }
  ?iso :binaryInteraction ?it, ?it2.
  ?it :interactant ?mitopartner; :quality :GOLD .
  ?mitopartner :isoform / :cellularComponent ?loc .
  ?loc :quality :GOLD; :term / :childOf ?mitloc .
  ?it2 :interactant ?partner; :quality :GOLD .
} group by ?entry ?gen ?mitcnt  having (count(distinct ?mitopartner) >= 20)
order by desc(?mitcnt)
