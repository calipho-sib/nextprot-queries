#id:NXQ_00252
#title:Protein kinases involved in cancer pathways according to wikipathways
#tags:federated query,function,pathway
#acs:P00519,P31749,P31751
#count:295
#time:2

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

select distinct ?entry ?gen ?pathwayname where {
service <http://sparql.wikipathways.org/sparql> {
SELECT DISTINCT ?pathwayname ?gen WHERE
{ 
  {?geneProduct a wp:Protein}
    union
  {?geneProduct a wp:GeneProduct}
  
  ?geneProduct rdfs:label ?gen . 
  ?geneProduct dcterms:isPartOf ?pathway . 
  ?pathway a wp:Pathway . 
  ?pathway wp:organism ?organism . 
  FILTER(contains(str(?organism),"9606")) 
  ?pathway dc:title ?pathwayname . 
  FILTER(regex(?pathwayname,"cancer","i")). }
}
?entry a :Entry .
?entry :gene / :name / rdfs:label ?gen .
?entry :isoform / :enzymeClassification / :term /:childOf cv:2_7_-_- . # protein kinase activity
}
order by ?gen
