#id:NXQ_00025
#title:Proteins with at least 50 interactors that are not associated with a disease
#comment:We exclude proteins that have either a disease annotation (Xref to Orphanet are considered  disease annotation) and proteins w Disease-category KWs
#tags:disease,PPI,tutorial
#acs:Q12906, ...
#count:2691
#time:32

select distinct ?entry where {
  ?entry :isoform ?iso.
  ?iso :binaryInteraction/:interactant ?interactant.
  
  # with no disease
  filter not exists {
   {
     ?iso :medical / rdf:type :Disease.
   } union {
     ?iso :uniprotKeyword / :term ?kw .
     ?kw :termType "Disease"^^xsd:string
     filter (?kw != cv:KW-0656)
   }
  }

} group by ?entry ?iso having (count(?interactant) >= 50)

