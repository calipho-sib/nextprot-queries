#id:NXQ_00282
#title:Proteins with an interaction mapping encompassing a coiled coil or bZip region  
#tags:PPI,region,domain,snorql-only,tutorial
#acs:O00291,O43889,P01100
#count:604
#time:3

select distinct ?entry (str(?gen) as ?gene) ?itlen ?coilen (str(?gen2) as ?partnergene) where {
  ?entry :isoform ?iso; :gene / :recommendedName / rdfs:label ?gen .
  ?iso :swissprotDisplayed true; :interactionMapping ?itmap .
  ?itmap :interactant ?entry2; :start ?its; :end ?ite .
  bind(?ite - ?its + 1 as ?itlen)
  ?entry2 :isoform? ?iso2; :gene / :recommendedName / rdfs:label ?gen2.
  ?iso2 :swissprotDisplayed true.
  {?iso :coiledCoilRegion ?coil . }
  union
  {?iso :region ?coil . 
   ?coil :term cv:DO-00078 # bzip
  }
  ?coil :start ?cs; :end ?ce .
  bind(?ce - ?cs + 1 as ?coilen)
  filter (?cs >= ?its -15 && ?ce <= ?ite + 15) # overlap
  filter(xsd:float(?itlen) / xsd:float(?coilen) < 2.0) # itlen no more than 2 coilen
} order by ?entry 