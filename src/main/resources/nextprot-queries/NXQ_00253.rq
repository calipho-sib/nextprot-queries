#id:NXQ_00253
#title:Human pathways in which at least one protein is mitochondrial GOLD
#tags:pathway,subcellular location,quality,federated query,snorql-only,tutorial
#acs:22q11.2 Deletion Syndrome... 
#count:792
#time:650

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX dcterms: <http://purl.org/dc/terms/>

select ?pathwayname (group_concat(distinct ?gen ; SEPARATOR = ',') as ?gene) where {
    service <http://sparql.wikipathways.org/sparql> {

        { ?geneProduct a wp:GeneProduct .  }
            union
        { ?geneProduct a wp:Protein .  }
        ?geneProduct rdfs:label ?genraw .
        bind (concat( ""^^xsd:string, ?genraw) as ?gen).
        filter(!regex(?gen,"[ a-z-]")). # ensures official gene names for subsequent neXtprot matching
        ?geneProduct dcterms:isPartOf ?pathway .
        ?pathway a wp:Pathway .
        ?pathway wp:organism ?organism .
        filter(contains(str(?organism),"9606"))
        ?pathway dc:title ?pathwayname .
    }

    ?entry a :Entry .
    ?entry :gene / :recommendedName / rdfs:label ?gen .
    ?entry :isoform / :cellularComponent ?loc .
    values ?mitoloc {cv:SL-0173 cv:GO_0005739 } # SL and GO values for mitochondrion
    ?loc :term / :childOf ?mitoloc.  # mitochondrion
    ?loc :evidence / :quality :GOLD .
    filter not exists {?loc :negativeEvidence ?negev} # No negative localization evidence
}
group by ?pathwayname
order by ?pathwayname
