#id:NXQ_00280
#title:Proteins interacting with at least 10 members of a protein family  
#tags:family,PPI,snorql-only,tutorial
#acs:P08238,Q15788,P48443 
#count:60

select distinct ?entry (str(?gen) as ?gene) (str(?xlab) as ?itfamily)  ?itcnt ?membercnt ((xsd:float(?itcnt) / xsd:float(?membercnt)) as ?ratio)  where {
 {
  select ?xterm ?xlab (count(distinct ?member) as ?membercnt) where {
  ?member :isoform? / :familyName /:term /:childOf ?xterm.
  ?xterm rdfs:label ?xlab .
  } group by ?xterm ?xlab
 } 
 {
  select ?xterm ?entry ?gen (count (distinct ?interactant) as ?itcnt) where {
  ?entry :isoform ?iso; :gene / :name / rdfs:label ?gen .
  ?iso :interaction ?it.
  ?it :quality :GOLD; :interactant ?interactant.
  ?interactant :familyName /:term /:childOf ?xterm . 
  } group by ?xterm ?entry ?gen having (count (distinct ?interactant) >= 10)	
 }
} 
order by desc(?ratio)
limit 60



