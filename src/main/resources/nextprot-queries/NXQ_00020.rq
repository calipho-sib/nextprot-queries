#id:NXQ_00020
#title:Proteins with at least 2 HPA antibodies whose genes are located on chromosome 21 and that are highly expressed according to IHC in heart
#tags:antibody,chromosomal location,expression,method,tutorial
#acs:P56181,P48449,Q8TCY5
#count:5
#time:0.9

select ?entry where {
  select distinct ?entry ?id where {
    ?entry :gene / :chromosome "21"^^xsd:string .
    ?entry :isoform / :expressionProfile ?s1.
    ?s1 :term / :childOf cv:TS-0445.
    ?s1 :evidence ?evi.
    ?evi :observedExpression :High.
    ?evi :evidenceCode cv:ECO_0001055.
    ?entry :isoform / :antibodyMapping ?map.
	 ?map :evidence / :reference / :accession ?id . 
  }
} group by ?entry having (count(?id)>=2)
