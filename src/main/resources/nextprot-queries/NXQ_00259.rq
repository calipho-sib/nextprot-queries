#id:NXQ_00259
#title:Proteins involved in diseases who'se phenotypic manifestations include long organs 
#tags:disease,federated query,phenotype,tutorial
#acs:A2RRP1,A6NCS4,NX_A6NI61 
#count:586
#time:5

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>

select distinct ?entry (str(?gen) as ?genx) ?umlsTerm where {
  service <http://rdf.disgenet.org/sparql/> {
    select distinct ?protein ?umlsTerm where {
    ?gda sio:SIO_000628 ?gene,?disease .
    ?disease a sio:SIO_010056 . # traits or phenotypes
    ?disease dcterms:title ?umlsTerm .
    filter(contains(str(?umlsTerm),"Long "))
        ?gene a ncit:C16612; sio:SIO_010078 / skos:exactMatch ?protein .
    filter(contains(str(?protein),"purl.uniprot"))
      } group by ?protein ?umlsTerm
    }
  BIND(IRI(replace(str(?protein),"purl","www")) AS ?unipage) .
  ?entry :swissprotPage ?unipage .
  ?entry :gene / :name ?gen .
} order by ?entry
