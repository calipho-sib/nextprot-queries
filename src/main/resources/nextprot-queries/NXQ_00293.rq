#id:NXQ_00293
#title:Glycosylation sites annotated by GlyConnect from a list of publications (PMIDs)
#tags:federated,glycosylation,PTM,publication,sequence,snorql-only
#acs:NX_P39060,NX_Q14624
#count:10
#time:1.89

PREFIX faldo:<http://biohackathon.org/resource/faldo#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX glycan:<http://purl.jp/bio/12/glyco/glycan#>
PREFIX glyco:<http://purl.jp/bio/12/glyco/conjugate#>

select distinct ?entry ?isoNP ?position where {

values ?pmid {"24884609" "10441114"} # pubmed ids, space- or linebreak-separated

bind(IRI(concat("http://www.ncbi.nlm.nih.gov/pubmed/",?pmid)) as ?pubIRI)
service <https://glyconnect.expasy.org/sparql> {
  ?ref_conjugate glyco:has_protein_part ?glycoprotein . 
  ?glycoprotein glyco:glycosylated_at / faldo:location ?glycosite .
  ?glycosite faldo:reference ?isoform ; faldo:position ?position .
  ?ref_conjugate glycan:published_in / foaf:primaryTopicOf ?pubIRI .
 }
BIND(IRI(replace(str(?isoform),"http://purl.uniprot.org/isoforms/","http://nextprot.org/rdf/isoform/NX_")) AS ?isoNP) . 
?entry :isoform ?isoNP .
} order by ?entry ?position
