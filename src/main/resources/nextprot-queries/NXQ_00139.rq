#id:NXQ_00139
#title:Protein kinases which are drug targets according to CHEMBL
#tags:drug,federated query,CHEMBL,tutorial
#acs:O00141,O00238,O00311
#count:512
#time:3.5
#comment: Allmost all kinases
PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>

select distinct ?entry ?gen where {
  SERVICE<http://www.ebi.ac.uk/rdf/services/chembl/sparql>{
        SELECT distinct ?protein WHERE {
  		?target cco:hasTargetComponent ?tarComp . 
  		?target cco:taxonomy <http://identifiers.org/taxonomy/9606> . # human protein target
		?tarComp cco:targetCmptXref ?protein . 
		?protein a cco:UniprotRef .
		?activity a cco:Activity ; cco:hasMolecule ?drug ; cco:hasAssay ?assay .
		?assay cco:hasTarget ?target .
        }
  	 }
  ?entry skos:exactMatch ?protein .	    
  ?entry :isoform / :keyword / :term cv:KW-0418. #kinase	
  ?entry :gene / :name ?gen.
}
