#id:NXQ_00094
#title:Proteins which are targets of antipsychotic drugs and expressed in brain
#comment:Federated query with DrugBank, the sparql endpoint hasn't been updated since 2011
#tags:drug,expression,federated query
#acs:P08183,P10635,P11229, ...
#count: 34
#time: 1

PREFIX drugbank: <http://wifo5-04.informatik.uni-mannheim.de/drugbank/resource/drugbank/>

select distinct ?entry where {
  service <http://wifo5-03.informatik.uni-mannheim.de/drugbank/sparql> {
    select distinct ?unipage WHERE {
    ?drug drugbank:drugCategory ?drugCat.
    ?drug drugbank:target ?target.
    ?target drugbank:swissprotPage ?unipage.
	filter(?drugCat = <http://wifo5-04.informatik.uni-mannheim.de/drugbank/resource/drugcategory/antipsychoticAgents> ||
		   ?drugCat = <http://wifo5-04.informatik.uni-mannheim.de/drugbank/resource/drugcategory/antipsychotics>)  
    }
  }
  ?entry :swissprotPage ?unipage.
  ?entry :isoform /:detectedExpression / :term /:childOf cv:TS-0095. # detected in brain
}

